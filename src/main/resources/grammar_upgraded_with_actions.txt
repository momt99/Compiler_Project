program -> #pstart declaration_list EOF #pend
declaration_list -> declaration_list declaration | ϵ
declaration -> type_specifier #pt ID #pt var_declaration_rest | type_specifier #pt ID #pt fun_declaration_rest
var_declaration_rest -> ; #var_dec | [ NUMBER #pt ] ; #arr_dec
type_specifier -> int | void
fun_declaration_rest -> #func_dec ( params ) #func_init compound_stmt #end_func
params -> void | void #pt ID #pt #param_var_dec param_mult_list_optional | void #pt ID #pt [ ] #param_arr_dec param_mult_list_optional | int #pt ID #pt #param_var_dec param_mult_list_optional | int #pt ID #pt [ ] #param_arr_dec param_mult_list_optional
param_mult_list_optional -> , param_list | ϵ
param_list -> param_list , param | param
param -> type_specifier #pt ID #pt #param_var_dec | type_specifier #pt ID #pt [ ] #param_arr_dec
compound_stmt -> { #begin_scope declaration_list statement_list } #end_scope
statement_list -> statement_list statement | ϵ
statement -> expression_stmt | compound_stmt | selection_stmt | iteration_stmt | return_stmt | switch_stmt
expression_stmt -> expression ; #pop_value | continue ; #continue | break ; #break | ;
selection_stmt -> if ( expression ) #save statement else #jpf_save statement #jp
iteration_stmt -> while #begin_while_scope #label ( expression ) #save statement #while #end_while_scope
return_stmt -> return ; #return | return expression ; #set_retval #return
switch_stmt -> switch #begin_switch_scope ( expression ) { #put_1 #save case_stmts default_stmt } #end_switch_scope
case_stmts -> case_stmts case_stmt | ϵ
case_stmt -> case NUMBER #pt #put_num #jpf_cmp_save : statement_list
default_stmt -> default : #jpf statement_list | ϵ #jpf
expression -> ID #pt #put_var = expression #assign | ID #pt [ expression ] #put_arr_elem = expression #assign | ID #pt [ expression ] #put_arr_elem simple_expression_by_id | ID #pt ( #find_func #start_args args ) #call simple_expression_by_id | ID #pt #put_var simple_expression_by_id | signed_factor_without_imm_id simple_expression_by_id
simple_expression_by_id -> term' additive_expression' compare_or_not
compare_or_not -> relop #pt additive_expression #compare | ϵ
relop -> < | ==
additive_expression -> term additive_expression'
additive_expression' -> addop #pt term #add_sub additive_expression' | ϵ
addop -> + | -
term -> signed_factor term'
term' -> * signed_factor #mult term' | ϵ
sign -> + | -
signed_factor -> factor | sign #pt factor #apply_sign
signed_factor_without_imm_id -> factor_without_id | sign #pt factor #apply_sign
factor -> ( expression ) | ID #pt call_var_unshared | NUMBER #pt #put_num
factor_without_id -> ( expression ) | NUMBER #pt #put_num
call_var_unshared -> ( #find_func #start_args args ) #call | [ expression ] #put_arr_elem | #put_var
args -> arg_list | ϵ
arg_list -> arg_list , expression | expression #put_arg