program -> declaration_list EOF
declaration_list -> declaration_list declaration | ϵ
declaration -> type_specifier ID var_declaration_rest | type_specifier ID fun_declaration_rest
var_declaration_rest -> ; | [ NUMBER ] ;
type_specifier -> int | void
fun_declaration_rest -> ( params ) compound_stmt
params -> void | void ID param_mult_list_optional | void ID [ ] param_mult_list_optional | int ID param_mult_list_optional | int ID [ ] param_mult_list_optional
param_mult_list_optional -> , param_list | ϵ
param_list -> param_list , param | param
param -> type_specifier ID | type_specifier ID [ ]
compound_stmt -> { declaration_list statement_list }
statement_list -> statement_list statement | ϵ
statement -> expression_stmt | compound_stmt | selection_stmt | iteration_stmt | return_stmt | switch_stmt
expression_stmt -> expression ; | continue ; | break ; | ;
selection_stmt -> if ( expression ) statement else statement
iteration_stmt -> while ( expression ) statement
return_stmt -> return ; | return expression ;
switch_stmt -> switch ( expression ) { case_stmts default_stmt }
case_stmts -> case_stmts case_stmt | ϵ
case_stmt -> case NUMBER : statement_list
default_stmt -> default : statement_list | ϵ
expression -> ID indexed_or_not assignment_or_not | signed_factor_without_imm_id term' additive_expression' compare_or_not
var -> ID | ID [ expression ]
indexed_or_not -> [ expression ] | ϵ
assignment_or_not -> = expression | simple_expression_by_id
simple_expression_by_id -> call_or_not term' additive_expression' compare_or_not
compare_or_not -> relop additive_expression | ϵ
call_or_not -> ( args ) | ϵ
relop -> < | ==
additive_expression -> term additive_expression'
additive_expression' -> addop term additive_expression' | ϵ
addop -> + | -
term -> signed_factor term'
term' -> * signed_factor term' | ϵ
sign -> + | -
signed_factor -> factor | sign factor
signed_factor_without_imm_id -> factor_without_id | sign factor
factor -> ( expression ) | ID call_var_unshared | NUMBER
factor_without_id -> ( expression ) | NUMBER
call -> ID ( args )
call_var_unshared -> ( args ) | [ expression ] | ϵ
args -> arg_list | ϵ
arg_list -> arg_list , expression | expression